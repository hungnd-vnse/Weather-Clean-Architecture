plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jmailen.kotlinter'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'com.google.protobuf'
}
android {
    namespace "com.example.weatherjourney"
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.nero.weatherjourney"
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
        versionCode 2
        versionName "0.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def properties = new Properties()
        properties.load(rootProject.file("local.properties").newDataInputStream())

        buildConfigField("String", "OPENCAGE_API_KEY",
                "\"${properties.getProperty("OPENCAGE_API_KEY")}\"")
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    buildFeatures {
        compose true
    }
    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    kotlinOptions {
        jvmTarget = 17
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$composeCompilerVersion"
    }
}

/*
 Dependency versions are defined in the top level build.gradle file. This helps keeping track of
 all versions in a single place. This improves readability and helps managing project complexity.
 */
dependencies {
    // Jetpack Compose
    implementation "androidx.compose.material3:material3:$composeMaterial3Version"
    implementation "androidx.navigation:navigation-compose:$navigationVersion"
    implementation "me.omico.lux:lux-androidx-compose-material3-pullrefresh"
    implementation "androidx.compose.ui:ui-tooling-preview:$composeVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$androidxLifecycleVersion"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanistVersion"
    implementation "com.google.accompanist:accompanist-permissions:$accompanistVersion"
    debugImplementation "androidx.compose.ui:ui-tooling:$composeVersion"

    // Splash Screen
    implementation "androidx.core:core-splashscreen:$splashScreenVersion"

    // Location Service
    implementation "com.google.android.gms:play-services-location:$locationServiceVersion"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    implementation "androidx.hilt:hilt-navigation-compose:$hiltAndroidxVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serializationJsonVersion"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:$serializationConverterVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"

    // DataStore
    implementation "androidx.datastore:datastore-preferences:$dataStoreVersion"
    implementation "androidx.datastore:datastore:$dataStoreVersion"

    // Protobuf
    implementation "com.google.protobuf:protobuf-javalite:$protobufVersion"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:$workManagerVersion"

    // Local Test
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    testImplementation "app.cash.turbine:turbine:$turbineVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"

    // Android Test
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$composeVersion"
}
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}